plugins {
  id 'fabric-loom' version '0.10-SNAPSHOT'
}

archivesBaseName = "$mod_name-fabric"

loom {
  runs {
    client {
      client()
      setConfigName "Fabric Client"
      ideConfigGenerated true
      runDir "run"
    }
    server {
      server()
      setConfigName "Fabric Server"
      ideConfigGenerated true
      runDir "run"
    }
  }
}

repositories {
  maven { url "https://maven.shedaniel.me" }
}

dependencies {
  implementation project(':common')

  minecraft "com.mojang:minecraft:$minecraft_version"
  mappings loom.layered() {
    officialMojangMappings()
    parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
  }

  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  modImplementation(include(fabricApi.module("fabric-resource-loader-v0", fabric_version)))

  modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
  modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"

  modRuntimeOnly("supercoder79:databreaker:0.2.8") {
    exclude module: "fabric-loader"
  }
}

processResources {
  from project(':common').sourceSets.main.resources

  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.source(project(':common').sourceSets.main.allSource)
}
