buildscript {
  repositories {
    maven { url "https://maven.minecraftforge.net" }
    maven { url "https://repo.spongepowered.org/maven" }
    mavenCentral()
  }
  dependencies {
    classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
    classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
  }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

archivesBaseName = "$mod_name-forge"
group = maven_group
version = "$mod_version+$minecraft_version"

java {
  toolchain.languageVersion = JavaLanguageVersion.of(8)
  withSourcesJar()
}

minecraft {
  mappings channel: "official", version: minecraft_version

  runs {
    client {
      taskName "client"
      workingDirectory project.file("run")
      ideaModule "${rootProject.name}.${project.name}.main"
      arg "-mixin.config=${mod_id}.mixins.json"

      mods {
        whats_that_slot {
          source sourceSets.main
        }
      }
    }

    server {
      taskName "server"
      workingDirectory project.file("run")
      ideaModule "${rootProject.name}.${project.name}.main"
      arg "-mixin.config=${mod_id}.mixins.json"

      mods {
        whats_that_slot {
          source sourceSets.main
        }
      }
    }

    data {
      taskName "data"
      workingDirectory project.file("run")
      ideaModule "${rootProject.name}.${project.name}.main"
      args "--mod", mod_id, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
      arg "-mixin.config=${mod_id}.mixins.json"

      mods {
        whats_that_slot {
          source sourceSets.main
        }
      }
    }
  }
}

sourceSets.main.resources.srcDir "src/generated/resources"

repositories {
  mavenCentral()
  maven { url "https://repo.spongepowered.org/repository/maven-public/" }
  maven { url "https://dvs1.progwml6.com/files/maven/" }
  maven { url "https://modmaven.dev" }
}

dependencies {
  minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

  compileOnly fg.deobf("mezz.jei:jei-$minecraft_version:$jei_version:api")
  runtimeOnly fg.deobf("mezz.jei:jei-$minecraft_version:$jei_version")

  annotationProcessor "org.spongepowered:mixin:0.8.3:processor"

  implementation "com.google.code.findbugs:jsr305:3.0.2"
}

mixin {
  add sourceSets.main, "${mod_id}.refmap.json"
}

jar.finalizedBy("reobfJar")

jar {
  manifest {
    attributes([
        "Specification-Title"     : mod_name,
        "Specification-Vendor"    : "Gegy",
        "Specification-Version"   : mod_version,
        "Implementation-Title"    : mod_name,
        "Implementation-Version"  : mod_version,
        "Implementation-Vendor"   : "Gegy",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "Timestamp"               : System.currentTimeMillis(),
        "MixinConfigs"            : "${mod_id}.mixins.json"
    ])
  }

  from("LICENSE") {
    rename { "${it}_${mod_name}" }
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
}

// fix for mixin annotation processing handling incorrectly in IDEA gradle sync
if (System.getProperty("idea.sync.active") == "true") {
  afterEvaluate {
    tasks.withType(JavaCompile).all {
      it.options.annotationProcessorPath = files()
    }
  }
}
