plugins {
  id 'java'
  id 'fabric-loom' version '0.7-SNAPSHOT'
}

group = maven_group
version = "$mod_version+$minecraft_version"
archivesBaseName = "$mod_name-fabric"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

java {
  withSourcesJar()
}

loom {
  runs {
    client {
      client()
      setConfigName "Fabric Client"
      ideConfigGenerated true
      runDir "run"
    }
    server {
      server()
      setConfigName "Fabric Server"
      ideConfigGenerated true
      runDir "run"
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  minecraft "com.mojang:minecraft:$minecraft_version"
  mappings loom.officialMojangMappings()

  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  modImplementation(include(fabricApi.module("fabric-resource-loader-v0", fabric_version)))

  implementation "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

jar {
  from("LICENSE") {
    rename { "${it}_${mod_name}" }
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
}
