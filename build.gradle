subprojects {
  apply plugin: 'java'

  group = maven_group
  version = "$mod_version+$minecraft_version" + (release ? "" : "-beta")

  java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
  }

  repositories {
    mavenCentral()
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url "https://maven.gegy.dev/" }
    maven { url "https://maven.parchmentmc.org/" }
  }

  dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
  }

  jar {
    manifest {
      attributes([
          'Specification-Title'     : mod_name,
          'Specification-Vendor'    : "Gegy",
          'Specification-Version'   : mod_version,
          'Implementation-Title'    : mod_name,
          'Implementation-Version'  : mod_version,
          'Implementation-Vendor'   : "Gegy",
          'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
          'Timestamp'               : System.currentTimeMillis(),
          "MixinConfigs"            : "${mod_id}.mixins.json"
      ])
    }

    from("LICENSE") {
      rename { "${it}_${mod_name}" }
    }
  }

  tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
  }

  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }
}
